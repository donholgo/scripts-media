#!/usr/bin/perl

use strict;
use warnings;
use File::Basename;

my $basename = basename $0;
my $usage = <<EOF;
Usage:
  $basename <shifttime> <file> ...

<shifttime> has the format
  [-][<years>y][<months>M][<days>d][<hours>h][<minutes>m][<seconds>s]
e.g.
  2d4h (2 days, 4 hours)
  -1h10m50s (minus 1 hour, 10 minutes, 50 seconds)

All components are optional, but at least one time part must be present.
EOF

die $usage unless @ARGV >= 2;

my $shifttime = shift;

my $sign = 1;

$shifttime =~ s/\s//g;
$shifttime =~ s/^\+//;

if ($shifttime =~ /^-(.*)/) {
  $sign = -1;
  $shifttime = $1;
}

die $usage unless $shifttime;

my @units = ('y', 'M', 'd', 'h', 'm', 's');
my @t;
foreach my $unit (@units) {
  if ($shifttime =~ /^(.*?)([\d]+)$unit(.*)$/) {
    push @t, $2;
    $shifttime = $1.$3;
  } else {
    push @t, 0;
  }
}

die $usage if $shifttime;
my $difftime = join(':', @t[0..2]).' '.join(':', @t[3..5]);

my $option = '-AllDates'.($sign == 1 ? '+' : '-').'='.$difftime;

foreach my $file (@ARGV) {
  print "$file\n";
  system('exiftool', '-preserve', '-quiet', $option, $file);   # -overwrite_original?
  my $original = $file.'_original';
  system('touch', '-r', $original, $file);
}
